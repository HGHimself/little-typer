#lang pie

(claim =consequence
  (-> Nat Nat
      U))

(define =consequence
  (lambda (n j)
    (which-Nat n
      (which-Nat j
        Trivial
        (lambda (j-1)
          Absurd))
      (lambda (n-1)
        (which-Nat j
          Absurd
          (lambda (j-1)
            (= Nat n-1 j-1)))))))

(claim =consequence-wrong
  (-> Nat Nat
      U))

(define =consequence-wrong
  (lambda (n j)
    (which-Nat n
      (which-Nat j
        Absurd
        (lambda (j-1)
          Trivial))
      (lambda (n-1)
        (which-Nat j
          Trivial
          (lambda (j-1)
            (= Nat n-1 j-1)))))))

(claim =consequence-same
  (Pi ((n Nat))
    (=consequence n n)))

(define =consequence-same
  (lambda (n)
    (ind-Nat n
      (lambda (k)
        (=consequence k k))
      sole
      (lambda (n-1 consequence-n-1)
        (same n-1)))))

(claim use-Nat=
  (Pi ((n Nat)
       (j Nat))
    (-> (= Nat n j)
        (=consequence n j))))

(define use-Nat=
  (lambda (n j)
    (lambda (n=j)
      (replace n=j
        (lambda (k)
          (=consequence n k))
        (=consequence-same n)))))

(claim zero-not-add1
  (Pi ((n Nat))
    (-> (= Nat zero (add1 n))
        Absurd)))

(define zero-not-add1
  (lambda (n)
    (use-Nat= zero (add1 n))))

(claim donut-absurdity
  (-> (= Nat 0 6)
      (= Atom 'powdered 'glazed)))

(define donut-absurdity
  (lambda (zero=six)
    (ind-Absurd
      (zero-not-add1 5 zero=six)
      (= Atom 'powdered 'glazed))))

(claim sub1=
  (Pi ((n Nat)
       (j Nat))
    (-> (= Nat (add1 n) (add1 j))
        (= Nat n j))))

(define sub1=
  (lambda (n j)
    (use-Nat= (add1 n) (add1 j))))

(claim one-not-six
  (-> (= Nat 1 6)
      Absurd))

(define one-not-six
  (lambda (one=six)
    (zero-not-add1 4
      (sub1= 0 5 one=six))))

(claim front
  (Pi ((E U)
       (n Nat))
    (-> (Vec E (add1 n))
        E)))

(claim mot-front
  (Pi ((E U)
       (k Nat))
    (-> (Vec E k)
        U)))

(define mot-front
  (lambda (E)
    (lambda (k es)
      (Pi ((j Nat))
        (-> (= Nat k (add1 j))
            E)))))

(claim step-front
  (Pi ((E U)
       (l Nat)
       (e E)
       (es (Vec E l)))
    (-> (mot-front E l es)
        (mot-front E (add1 l) (vec:: e es)))))

(define step-front
  (lambda (E l e es)
    (lambda (front-es)
      (lambda (j eq)
        e))))

(define front
  (lambda (E l es)
    ((ind-Vec (add1 l) es
       (mot-front E)
       (lambda (j eq)
         (ind-Absurd
           (zero-not-add1 j eq)
           E))
       (step-front E))
      l (same (add1 l)))))

(claim rest
  (Pi ((E U)
       (n Nat))
    (-> (Vec E (add1 n))
        (Vec E n))))

(claim mot-rest
  (Pi ((E U)
       (k Nat))
    (-> (Vec E k)
        U)))

(define mot-rest
  (lambda (E)
    (lambda (k es)
      (Pi ((j Nat))
        (-> (= Nat k (add1 j))
            (Vec E j))))))

(claim step-rest
  (Pi ((E U)
       (l Nat)
       (e E)
       (es (Vec E l)))
    (-> (mot-rest E l es)
        (mot-rest E (add1 l) (vec:: e es)))))

(define step-rest
  (lambda (E l e es)
    (lambda (rest-es)
      (lambda (j eq)
        (replace (sub1= l j eq)
          (lambda (j-1)
            (Vec E j-1))
          es)))))

(claim absurd-base
  (Pi ((E U)
       (j Nat))
    (-> (= Nat 0 (add1 j))
        (Vec E j))))

(define absurd-base
  (lambda (E j eq)
    (ind-Absurd
      (zero-not-add1 j eq)
      (Vec E j))))

(define rest
  (lambda (E l es)
    ((ind-Vec (add1 l) es
       (mot-rest E)
       (lambda (j eq)
         (ind-Absurd
           (zero-not-add1 j eq)
           (Vec E j)))
       (step-rest E))
      l (same (add1 l)))))

(claim ident-Nat
  (-> Nat
      Nat))

(define ident-Nat
  (lambda (n)
    n))

(claim dec1
  (-> Nat
      Nat))

(define dec1
  (lambda (n)
    (which-Nat n
      zero
      ident-Nat)))

(claim verbose-add1
  (-> Nat
      Nat))

(define verbose-add1
  (lambda (n)
    (add1 (dec1 (add1 n)))))

(claim add1-dec1-3=3
  (= Nat 3 (add1 (dec1 3))))

(define add1-dec1-3=3
  (same 3))

(claim add1-dec1=same
  (Pi ((n Nat))
    (= Nat
       (add1 n)
       (add1 (dec1 (add1 n))))))

(define add1-dec1=same
  (lambda (n)
    (same (add1 (dec1 (add1 n))))))

(claim add1-dec1=
  (Pi ((l Nat)
       (j Nat))
    (-> (= Nat l j)
      (= Nat
        (add1 l)
        (add1 (dec1 (add1 j)))))))

(define add1-dec1=
  (lambda (l j)
    (lambda (eq)
      (cong eq verbose-add1))))

(claim drop-last
  (Pi ((E U)
       (l Nat))
    (-> (Vec E (add1 l))
        (Vec E l))))

(claim mot-drop-last
  (Pi ((E U)
       (k Nat))
    (-> (Vec E k)
        U)))

(define mot-drop-last
  (lambda (E k)
    (lambda (es)
      (-> (Vec E (add1 k))
        (Vec E k)))))

(claim base-drop-last
  (Pi ((E U))
    (mot-drop-last E zero vecnil)))

(define base-drop-last
  (lambda (E)
    (lambda (es)
      vecnil)))

(claim step-drop-last
  (Pi ((E U)
       (l Nat)
       (e E)
       (es (Vec E l)))
    (-> (mot-drop-last E l es)
        (mot-drop-last E (add1 l) (vec:: e es)))))

(define step-drop-last
  (lambda (E l e es)
    (lambda (drop-last-l-1)
      (lambda (ease)
        (vec:: (head ease)
          (drop-last-l-1 (tail ease)))))))

; ha this is finally done, looks like I had to be a little sneaky though
(define drop-last
  (lambda (E l es)
    (tail ((ind-Vec (add1 l) es
      (mot-drop-last E)
      (base-drop-last E)
      (step-drop-last E))
    (vec:: (head es) es)))))

(claim vector
  (Vec Atom 3))

(define vector
  (vec:: 'apple
    (vec:: 'banana
      (vec:: 'clementine
        vecnil))))

(claim pem-not-false
  (Pi ((X U))
    (-> (-> (Either X
              (-> X
                Absurd))
          Absurd)
      Absurd)))

(define pem-not-false
  (lambda (X)
    (lambda (pem-false)
      (pem-false
        (right
          (lambda (x)
            (pem-false
              (left x))))))))

(claim Dec
  (-> U
      U))

(define Dec
  (lambda (X)
    (Either X
      (-> X
          Absurd))))